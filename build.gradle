buildscript {
  ext {
    indraVersion = '1.2.1'
  }
}

plugins {
  id 'net.kyori.indra' version "$indraVersion" apply false
  id 'net.kyori.indra.publishing.sonatype' version "$indraVersion" apply false
  id 'net.kyori.indra.license-header' version "$indraVersion" apply false
}

// Adventure version
ext.adventure = "4.2.0"

subprojects {
  apply plugin: 'net.kyori.indra'
  apply plugin: 'net.kyori.indra.publishing.sonatype'
  apply plugin: 'net.kyori.indra.checkstyle'
  apply plugin: 'net.kyori.indra.license-header'

  group 'net.kyori'
  version '4.0.0-SNAPSHOT'
  description 'Platform integrations for the adventure UI library'

  repositories {
    mavenLocal()
    mavenCentral()
    sonatypeSnapshots()
    maven {
      name = 'viaversion'
      url = 'https://repo.viaversion.com'
    }
  }

  dependencies {
    checkstyle 'ca.stellardrift:stylecheck:0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
  }

  indra {
    javaVersions {
      testWith(8, 11, 15)
    }

    github("KyoriPowered", "adventure-platform") {
      ci = true
    }
    mitLicense()

    configurePublications {
      pom {
        developers {
          developer {
            id = 'kashike'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'lucko'
            name = 'Luck'
            url = 'https://lucko.me'
            email = 'git@lucko.me'
          }

          developer {
            id = 'zml'
            name = 'zml'
            timezone = 'America/Vancouver'
          }

          developer {
            id = 'Electroid'
          }
        }
      }
    }
  }
}
